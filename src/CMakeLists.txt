set(UBITRACK_LIBRARY_NAME utcore)


# Instrumentation support for utcore library
OPTION(ENABLE_TRACING "Enable instrumentation support?" OFF)

# Instrumentation of utcore
SET(INSTRUMENTATION_SOURCE_FOLDER "${CMAKE_SOURCE_DIR}/misc/tracing")

SET(ENABLE_TRACING_DTRACE OFF)
SET(ENABLE_TRACING_ETW OFF)
SET(ENABLE_TRACING_LTTNGUST OFF)
SET(ENABLE_TRACING_USDT OFF)

SET(TRACING_HDR_FILES "")
SET(TRACING_SRC_FILES "")
SET(TRACING_EXTRA_LIBRARIES "")
SET(TRACING_INCLUDE_DIRS "")

# Tracing Providers for various platforms
IF(APPLE AND ENABLE_TRACING)
    MESSAGE( STATUS "Enabled DTrace.")
    INCLUDE(dtrace)
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "HAVE_DTRACE")
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "ENABLE_EVENT_TRACING")
    SET(TRACING_INCLUDE_DIRS ${TRACING_INCLUDE_DIRS} ${CMAKE_BINARY_DIR})
    SET(ENABLE_TRACING_DTRACE ON)
ENDIF(APPLE AND ENABLE_TRACING)

IF(WIN32 AND ENABLE_TRACING)
    MESSAGE( STATUS "Enabled Event Tracing for Windows.")
    INCLUDE(etw)
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "HAVE_ETW")
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "ENABLE_EVENT_TRACING")
    SET(TRACING_INCLUDE_DIRS ${TRACING_INCLUDE_DIRS} ${CMAKE_BINARY_DIR})
    SET(ENABLE_TRACING_ETW ON)
ENDIF(WIN32 AND ENABLE_TRACING)

IF(CMAKE_SYSTEM_NAME MATCHES "Linux" AND ENABLE_TRACING)

    # MESSAGE( STATUS "Enabled DTrace.")
    # INCLUDE(dtrace)
    # set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "HAVE_DTRACE")
    # set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "ENABLE_EVENT_TRACING")
    # SET(TRACING_INCLUDE_DIRS ${TRACING_INCLUDE_DIRS} ${CMAKE_BINARY_DIR})
    # SET(ENABLE_TRACING_DTRACE ON)

    MESSAGE( STATUS "Enabled USDT.")
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "HAVE_USDT")
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "ENABLE_EVENT_TRACING")
    SET(ENABLE_TRACING_USDT ON)

# example provided here:
# https://github.com/lttng/lttng-ust/tree/master/doc/examples/cmake-multiple-shared-libraries
   # MESSAGE( STATUS "Enabled LTTNG-UST.")
   # include (FindLTTngUST REQUIRED)
   # set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "HAVE_LTTNGUST")
   # set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "ENABLE_EVENT_TRACING")
   # SET(ENABLE_TRACING_LTTNGUST ON)
ENDIF(CMAKE_SYSTEM_NAME MATCHES "Linux" AND ENABLE_TRACING)


IF(ENABLE_TRACING_DTRACE)
    set(TRACING_HDR_FILES ${TRACING_HDR_FILES} "${CMAKE_BINARY_DIR}/utUtil/probes_ubitrack_dtrace.h")
ENDIF(ENABLE_TRACING_DTRACE)

IF(ENABLE_TRACING_ETW)
    set(TRACING_HDR_FILES ${TRACING_HDR_FILES} "${CMAKE_BINARY_DIR}/utUtil/probes_ubitrack_etw.h")
    set(TRACING_SRC_FILES ${TRACING_SRC_FILES} "${CMAKE_BINARY_DIR}/utUtil/probes_ubitrack_etw.rc")
ENDIF(ENABLE_TRACING_ETW)

IF(ENABLE_TRACING_LTTNGUST)
    SET(TRACING_INCLUDE_DIRS ${TRACING_INCLUDE_DIRS} ${LTTNGUST_INCLUDE_DIRS})
    set(TRACING_EXTRA_LIBRARIES LTTng::UST)
ENDIF(ENABLE_TRACING_LTTNGUST)


# Source and Header Files

# All cpp files for UTCORE
FILE(GLOB UBITRACK_SRCS RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}"
   "${CMAKE_CURRENT_SOURCE_DIR}/*.cpp"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*.cpp"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*.cpp"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*/*.cpp"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*/*/*.cpp"
)

# All header files for UTCORE
FILE(GLOB UBITRACK_HEADERS RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}"
   "${CMAKE_CURRENT_SOURCE_DIR}/*.h"  
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*.h" 
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*.h"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*/*.h"
   "${CMAKE_CURRENT_SOURCE_DIR}/*/*/*/*/*.h"
)

# UTCORE library definition
ADD_LIBRARY(${UBITRACK_LIBRARY_NAME} ${UBITRACK_SRCS} ${TRACING_SRC_FILES} ${UBITRACK_HEADERS} ${TRACING_HDR_FILES})

target_link_libraries(${UBITRACK_LIBRARY_NAME} PUBLIC 
  CONAN_PKG::Boost 
  CONAN_PKG::clapack 
  CONAN_PKG::msgpack 
  CONAN_PKG::ubitrack_boost_bindings 
  CONAN_PKG::ubitrack_tinyxml
  CONAN_PKG::ubitrack_log4cpp
  PRIVATE ${TRACING_EXTRA_LIBRARIES})

target_include_directories(${UBITRACK_LIBRARY_NAME} PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
  $<INSTALL_INTERFACE:include>
  PRIVATE src ${TRACING_INCLUDE_DIRS})

if(WIN32)
  IF(BUILD_SHARED_LIBS)
    string(TOUPPER "${UBITRACK_LIBRARY_NAME}" _libname_upper)
    set(UBITRACK_COMPILE_DEFINITIONS ${UBITRACK_COMPILE_DEFINITIONS} "${_libname_upper}_DLL")
  ENDIF(BUILD_SHARED_LIBS)
endif(WIN32)

# Apply Target Properties
if(MSVC)
  if(CMAKE_CROSSCOMPILING)
    set_target_properties(${UBITRACK_LIBRARY_NAME} PROPERTIES LINK_FLAGS "/NODEFAULTLIB:secchk")
  endif()
  set_target_properties(${UBITRACK_LIBRARY_NAME} PROPERTIES LINK_FLAGS "/NODEFAULTLIB:libc /DEBUG")
endif()

ubitrack_set_target_properties(${UBITRACK_LIBRARY_NAME})

# Generate tracing helper files
IF(ENABLE_TRACING_DTRACE)
    DTRACE_INSTRUMENT(${UBITRACK_LIBRARY_NAME})
ENDIF(ENABLE_TRACING_DTRACE)

IF(ENABLE_TRACING_ETW)
    ETW_INSTRUMENT(${UBITRACK_LIBRARY_NAME})
ENDIF(ENABLE_TRACING_ETW)

ubitrack_install_headers(${UBITRACK_HEADERS} ${TRACING_HDR_FILES})
ubitrack_install_library(${UBITRACK_LIBRARY_NAME})
