import sys
import glob

# import variables from other scons files
Import( '*' )
env = masterEnv.Clone()
env.AppendUnique( **mergeOptions( utcore_all_options ) )

# automatically glob files from subdirectories
subdirs = [ 'Math', 'Geometry', 'Stochastic', 'Calibration' ]

headers = []
sources = []

for dir in subdirs:
	headers += globSourceFiles( dir + '/*.h' )
	sources += globSourceFiles( dir + '/*.cpp' )

# add the main file
sources += [ 'main.cpp', 'boost_unit_test_framework.cpp' ]

# build the test program
runtests = env.Program( 'runtests', sources )

# tests need an installed ubitrack
env.Depends( runtests, 'install-all' )

# also create a visual studio project for the library
createVisualStudioProject(env, sources, headers, 'utCore-Tests')

# run tests if specified on the scons command line
###if 'test' in COMMAND_LINE_TARGETS:
test = env.Test( "runtest.passed", runtests, ARGS = [ "--log_level=test_suite" ] )
env.Alias( [ 'test', 'test-core' ], test )
